*** Settings ***

Library  Selenium2Library  timeout=10  implicit_wait=0.5
Library  plone.app.collection.testing_keywords.Keywords

Resource  plone/act/keywords.txt

Variables  plone/app/testing/interfaces.py

Suite Setup  Suite Setup
Suite Teardown  Suite Teardown


*** Variables ***

${front-page}  http://localhost:55001/plone
${test-folder}  http://localhost:55001/plone

${PORT} =  55001
${ZOPE_URL} =  http://localhost:${PORT}
${PLONE_URL} =  ${ZOPE_URL}/plone
${BROWSER} =  Firefox


*** Test Cases ***

Scenario: Title Criterion
    Given a logged-in adminstrator
     And a collection  My Collection
     And a document  My document

    When I set the collection title criterion to  My document

    Then the collection should show one result
     And the collection should contain  My document


*** Keywords ***

Suite Setup
    Open browser  ${front-page}

Suite Teardown
    Close All Browsers

a logged-in adminstrator
    Log in as site owner

a document
    [Arguments]  ${title}
    Go to  ${test-folder}/createObject?type_name=Document
    Input text  name=title  ${title}
    Click Button  Save

a collection
    [Arguments]  ${title}
    Go to  ${test-folder}/createObject?type_name=Collection
    Input text  name=title  ${title}
    Click Button  Save

I set the collection title criterion to
    [Arguments]  ${title_criterion}
    Go to  ${front-page}/my-collection/edit
    Wait Until Page Contains Element  xpath=//select[@name="addindex"]
    Select From List  xpath=//select[@name="addindex"]  Title
    Wait Until Page Contains Element  xpath=//select[@class='queryoperator']
    Input Text  name=query.v:records  ${title_criterion}
    Wait Until Page Contains  1 items matching your search terms.
    Page should contain  ${title_criterion}
    Click Button  Save

the collection should show one result
    Go to  ${front-page}/my-collection
    # XXX: needs to be fixed.
    #Page should contain element  xpath=count(//span[@class="summary"])==1

the collection should contain
    [Arguments]  ${title}
    Go to  ${front-page}/my-collection
    Page should contain  ${title}
