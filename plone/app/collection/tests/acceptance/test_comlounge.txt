*** Settings ***

Library  Selenium2Library  timeout=10  implicit_wait=0.5
Library  plone.app.collection.testing_keywords.Keywords

Resource  plone/act/keywords.txt
Resource  collection_keywords.txt

Variables  plone/app/testing/interfaces.py

Suite Setup  Suite Setup
Suite Teardown  Suite Teardown

*** Variables ***

${front-page}  http://localhost:55001/plone/
${test-folder}  http://localhost:55001/plone/acceptance-test-folder

${PORT} =  55001
${ZOPE_URL} =  http://localhost:${PORT}
${PLONE_URL} =  ${ZOPE_URL}/plone
${BROWSER} =  Firefox


*** Test Cases ***

Test Double Relative Location Criterion
    Log in as site owner
    Go to  ${test-folder}
    Add folder  My Folder 1
    Add Page  Document inside My Folder 1
    Go to  ${test-folder}
    Add folder  My Folder 2
    Add Page  Document inside My Folder 2

    Go to  ${test-folder}
    Create Collection  Location Criteria Collection

    Click Edit In Edit Bar
    Set Relative Location Criterion To  ../my-folder-1/
    Click Edit In Edit Bar
    Set Relative Location Criterion To  ../my-folder-2/

    Page should contain  Document inside My Folder 1
    Page should contain  Document inside My Folder 2


Test Absolute Location Criterion
    Log in as site owner
    Go to  ${test-folder}
    Add folder  My Folder
    Add Page  Document inside My Folder

    Go to  ${test-folder}
    Create Collection  Location Criteria Collection

    Click Edit In Edit Bar
    Set Absolute Location Criterion To  ../my-folder/
    Click Button  Save

    Page should contain  Document inside My Folder