*** Settings ***

Library  Selenium2Library  timeout=10  implicit_wait=0.5
Library  plone.app.collection.testing_keywords.Keywords

Suite Setup  Suite Setup
Suite Teardown  Suite Teardown

*** Variables ***

${front-page}  http://localhost:55001/plone/
${test-folder}  http://localhost:55001/plone/acceptance-test-folder


*** Test Cases ***

#Test Description Criterion
#Test Searchable text Criterion
#Test Tag Criterion
#Test Title Criterion

#Test Creation date Criterion
#Test Effective date Criterion
#Test Event end date Criterion
#Test Event start date Criterion
#Test Expiration date Criterion
#Test Modification date Criterion

#Test Creator Criterion
#Test Location Criterion

Test Review state Criterion
    Log in as site owner
    Create Document  Published Document
    Publish Object
    Create Document  Private Document
    Create Collection  My Collection

    Go to  ${test-folder}/my-collection/edit
    Set Review state Criterion

    Live Preview number of results should be  1
    Live Preview should contain  Published Document

    Page should contain  Published Document
    Page should not contain  Private Document

Test Short name (id) Criterion
    Log in as site owner
    Create Document  First Document
    Create Document  Second Document
    Create Collection  My Collection

    Go to  ${test-folder}/my-collection/edit
    Set Short name (id) Criterion

    Page should contain  First Document
    Page should not contain  Second Document

Test Type Criterion
    Log in as site owner
    Create Document  My Document
    Create News Item  My News Item
    Create Collection  My Collection

    Go to  ${test-folder}/my-collection/edit
    Set Type Criterion

    Page should contain  My Document
    Page should not contain  My News Item

#Test Type Criterion
#    Given a logged in user
#        And a document  My Document
#        And a news item  My News Item
#        And a collection  My Collection
#    When
#        I go to ${test-folder}/my-collection/edit
#        And set the type criterion to  Document
#    Then
#        The page should contain  My Document

*** Keywords ***

Suite Setup
    Open browser  ${front-page}

Suite Teardown
    Close All Browsers

Log in
    ${TEST_USER_NAME} =  Get test user name
    ${TEST_USER_PASSWORD} =  Get test user password
    Go to  ${front-page}/login_form
    Page should contain element  __ac_name
    Input text  __ac_name  ${TEST_USER_NAME}
    Input text  __ac_password  ${TEST_USER_PASSWORD}
    Click Button  Log in
    Page should contain element  css=#user-name

Log in as site owner
    ${TEST_USER_NAME} =  Get test site owner name
    ${TEST_USER_PASSWORD} =  Get test site owner password
    Go to  ${front-page}/login_form
    Page should contain element  __ac_name
    Input text  __ac_name  ${TEST_USER_NAME}
    Input text  __ac_password  ${TEST_USER_PASSWORD}
    Click Button  Log in
    Page should contain element  css=#user-name

Log out
    Go to  ${front-page}/logout
    Page should contain logged out

Create Document
    [Arguments]  ${title}
    Go to  ${test-folder}/createObject?type_name=Document
    Input text  title  ${title}
    Click Button  Save
    Page should contain  ${title}

Publish Object
    Click Element  xpath=//span[@class='state-private']
    Wait Until Page Contains  Publish
    Click Element  xpath=//span[text()="Publish"]
    Wait Until Page Contains  Published

Create News Item
    [Arguments]  ${title}
    Go to  ${test-folder}/createObject?type_name=News+Item
    Input text  title  ${title}
    Click Button  Save

Create Collection
    [Arguments]  ${title}
    Go to  ${test-folder}/++add++Collection
    Input text  name=form.widgets.IBasic.title  ${title}
    Click Button  Save

Set Review state Criterion
    Select From List  xpath=//select[@name="addindex"]  Review state
    Wait Until Page Contains Element  xpath=//select[@class='queryoperator']
    Select Checkbox  published

Live Preview number of results should be
    [Arguments]  ${number}
    Wait Until Page Contains  ${number} items matching your search terms.

Live Preview should contain
    [Arguments]  ${title}
    Page should contain  ${title}

Set Short name (id) Criterion
    Select From List  xpath=//select[@name="addindex"]  Short name (id)
    Wait Until Page Contains Element  xpath=//select[@class='queryoperator']
    Input Text  name=form.widgets.query.v:records  first-document
    Wait Until Page Contains  1 items matching your search terms.
    Page should contain  First Document
    Click Button  Save

Set Type Criterion
    Select From List  xpath=//select[@name="addindex"]  Type
    Wait Until Page Contains Element  xpath=//select[@class='queryoperator']
    Select Checkbox  Document
    Wait Until Page Contains  1 items matching your search terms.
    Page should contain  My Document
    Click Button  Save

Add collection
    Go to  ${test-folder}/my-collection/edit
    Click Link  xpath=//input[@name='form.widgets.query.v:records:list']
    Click Link  collection
    Page should contain  Add Collection
    Page should contain  Description
    Page should contain  Search terms

    Input text  form.widgets.IBasic.title  My Collection
    Select From List  xpath=//select[@name="addindex"]  Description
    Input text  form.widgets.query.v:records  Plone

    Click Button  Save
    Page should contain  My Collection
